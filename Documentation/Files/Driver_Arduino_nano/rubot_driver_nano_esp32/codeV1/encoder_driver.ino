/* *************************************************************
   Encoder definitions
   
   Add an "#ifdef" block to this file to include support for
   a particular encoder board or library. Then add the appropriate
   #define near the top of the main ROSArduinoBridge.ino file.
   
   ************************************************************ */


#ifdef USE_BASE

#ifdef ROBOGAIA
  /* The Robogaia Mega Encoder shield */
  #include "MegaEncoderCounter.h"

  /* Create the encoder shield object */
  MegaEncoderCounter encoders = MegaEncoderCounter(4); // Initializes the Mega Encoder Counter in the 4X Count mode
  
  /* Wrap the encoder reading function */
  long readEncoder(int i) {
    if (i == LEFT) return encoders.YAxisGetCount();
    else return encoders.XAxisGetCount();
  }

  /* Wrap the encoder reset function */
  void resetEncoder(int i) {
    if (i == LEFT) return encoders.YAxisReset();
    else return encoders.XAxisReset();
  }
#elif defined(ARDUINO_ENC_COUNTER)
  volatile long left_enc_pos = 0L;
  volatile long right_enc_pos = 0L;
  static const int8_t ENC_STATES [] = {0,1,-1,0,-1,0,0,1,1,0,0,-1,0,-1,1,0};  //encoder lookup table
    
  /* Interrupt routine for LEFT encoder, taking care of actual counting */
  ISR (PCINT2_vect){
  	static uint8_t enc_last=0;
        
	enc_last <<=2; //shift previous state two places
	enc_last |= (PIND & (3 << 2)) >> 2; //read the current state into lowest 2 bits
  
  	left_enc_pos += ENC_STATES[(enc_last & 0x0f)];
  }
  
  /* Interrupt routine for RIGHT encoder, taking care of actual counting */
  ISR (PCINT1_vect){
        static uint8_t enc_last=0;
          	
	enc_last <<=2; //shift previous state two places
	enc_last |= (PINC & (3 << 4)) >> 4; //read the current state into lowest 2 bits
  
  	right_enc_pos += ENC_STATES[(enc_last & 0x0f)];
  }
  
  /* Wrap the encoder reading function */
  long readEncoder(int i) {
    if (i == LEFT) return left_enc_pos;
    else return right_enc_pos;
  }

  /* Wrap the encoder reset function */
  void resetEncoder(int i) {
    if (i == LEFT){
      left_enc_pos=0L;
      return;
    } else { 
      right_enc_pos=0L;
      return;
    }
  }

#elif defined(ESP32_ENC_COUNTER)

volatile long left_enc_pos = 0L;
volatile long right_enc_pos = 0L;

// Define los pines de tus encoders (ajústalos según tu conexión)
const int leftPinA  = 47; // Left Encoder of motorA/1
const int leftPinB  = 21; // Left Encoder of motorB/2
const int rightPinA = 38; // Right Encoder of motorA/1
const int rightPinB = 18; // Right Encoder of motorB/2

// Rutina de interrupción para canal A izquierdo
void IRAM_ATTR handleLeftA() {
  bool b = digitalRead(leftPinB);
  left_enc_pos += b ? 1 : -1;
}

// Rutina de interrupción para canal A derecho
void IRAM_ATTR handleRightA() {
  bool b = digitalRead(rightPinB);
  right_enc_pos += b ? 1 : -1;
}

// Función de inicialización (deberás llamarla desde tu setup)
void setupEncoders() {
  pinMode(leftPinA, INPUT_PULLUP);
  pinMode(leftPinB, INPUT_PULLUP);
  pinMode(rightPinA, INPUT_PULLUP);
  pinMode(rightPinB, INPUT_PULLUP);

  attachInterrupt(leftPinA, handleLeftA, CHANGE);
  attachInterrupt(rightPinA, handleRightA, CHANGE);
}

// Lectura de posición
long readEncoder(int i) {
  if (i == LEFT) return left_enc_pos;
  else return right_enc_pos;
}

// Reset de contador individual
void resetEncoder(int i) {
  if (i == LEFT) {
    left_enc_pos = 0L;
  } else {
    right_enc_pos = 0L;
  }
}


#else
  #error A encoder driver must be selected!
#endif

/* Wrap the encoder reset function */
void resetEncoders() {
  resetEncoder(LEFT);
  resetEncoder(RIGHT);
}

#endif



